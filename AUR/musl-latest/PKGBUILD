# Maintainer: Steven Honeyman <stevenhoneyman at gmail com>

#
# This is the LATEST branch, unlike the STABLE 'musl' package
# also includes both static and shared libraries
#

pkgname=musl-latest
pkgver=1.1.2
pkgrel=1
pkgdesc="An implementation of the C/POSIX standard library - LATEST branch"
arch=('i686' 'x86_64')
url="http://www.musl-libc.org/"
license=('MIT')
conflicts=('musl' 'musl-static' 'musl-git')
options=('staticlibs')
source=(http://www.musl-libc.org/releases/musl-$pkgver.tar.gz)
md5sums=('a81309e54efdf9c68baf679790fc5678')

build() {
  cd "$srcdir/musl-$pkgver"

  # 2014-06-13 - configure failed without this extra CFLAG
  export CFLAGS="${CFLAGS} -fno-toplevel-reorder"

  ./configure --prefix=/usr/musl --bindir=/usr/bin --syslibdir=/usr/lib --libdir=/usr/lib/musl --includedir=/usr/include/musl
  make
}

package() {
  cd "$srcdir/musl-$pkgver"
  make DESTDIR="$pkgdir" install
}
