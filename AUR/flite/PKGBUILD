# Maintainer: Steven Honeyman <stevenhoneyman at gmail com>

pkgname=flite
pkgver=1.9.0
pkgrel=1
pkgdesc="A lighweight speech synthesis engine (text to speech)"
arch=('i686' 'x86_64')
url="http://cmuflite.org"
license=('custom')
depends=('alsa-lib')
options=('strip' '!buildflags')
source=(http://festvox.org/bard/${pkgname}-${pkgver}-current.tar.bz2)
md5sums=('6bf29408343a37532e4a3cdb280f72f4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}-current"
  sed '/^#VOXES.*$/d; s/+//g; s/usenglish/&\nVOXES = cmu_us_kal16 cmu_us_slt/' config/android.lv >config/archlinux.lv
  sed -i '/$(INSTALL) -m 755 $(BINDIR)\/flite_time $(DESTDIR)$(INSTALLBINDIR)/d' main/Makefile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-current"
  ./configure --prefix=/usr --enable-shared \
			    --with-audio=alsa \
	                    --with-vox=cmu_us_kal16 \
			    --with-langvox=archlinux
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-current"
  make prefix="${pkgdir}/usr" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" 
}
