# Maintainer: Steven Honeyman <stevehoneyman at gmail com>
# Contributor: David J. Haines <djhaines@gmx.com>

pkgname=txt2html
pkgver=2.5201
pkgrel=2
pkgdesc="Convert plain text files to HTML"
arch=('i686' 'x86_64')
url="http://txt2html.sourceforge.net/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-getopt-argvfile')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/R/RU/RUBYKAT/txt2html-2.5201.tar.gz)
md5sums=('44029f55c30a551f343b9424a98a3691')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/^use strict;$/&\nour $VERSION;/' scripts/txt2html
  sed -i 's/use YAML/#use YAML/g' lib/HTML/TextToHTML.pm
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  /usr/bin/perl Build.PL --installdirs vendor
  ./Build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  ./Build install --destdir "$pkgdir" --install_path script=usr/bin
}
